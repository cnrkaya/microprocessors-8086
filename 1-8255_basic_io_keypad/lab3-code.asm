;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Pzt Eki 7 2019
; Processor: 8086
; Compiler:  MASM32
;
; Before starting simulation set Internal Memory Size 
; in the 8086 model properties to 0x10000
;====================================================================

CODE    SEGMENT PARA 'CODE'
        ASSUME CS:CODE, DS:DATA, SS:STAK
	
STAK    SEGMENT PARA STACK 'STACK'
        DW 20 DUP(?)
STAK    ENDS

DATA    SEGMENT PARA 'DATA'
DIGITS  DB 0C0H, 0F9H, 0A4H, 0B0H, 99H, 92H, 82H, 0F8H, 80H, 98H ,00H, 0C0H;array definition
INPUTS  DB 00H,00H,00H,00H
TOTAL   DB 00H
DATA    ENDS

START PROC

MOV AX, DATA
MOV DS, AX

MOV AL,81H  ;89H,81H FARKETMEZ
OUT 0AFH,AL ;CONTROL WORD
MOV TOTAL,0
MAINLOOP:


MOV SI,1  ;DIS DONGU ADIMI
MOV BX,0080H  ;SUTUN TARAMA DEGERI
L1 :
   
   ;7 SEGMENT DISPLAY E YAZDIRMA
   CMP TOTAL,4
   JNE UYGUN_DEGIL
   PUSH BX
   PUSH SI
   CALL SAYILARI_YAZDIR
   POP SI 
   POP BX
   UYGUN_DEGIL:

   MOV AL,BL		
   OUT 0ABH, AL ; PORT B 
   
   ;TUS OKU
   IN AL, 0ADH  ;PORT C
   
   MOV CX,4 
   MOV DX, 0001H ; SATIR TARAMA DEGERI
   L2:
       
      TEST AL,DL
      JNZ TUS_ALGILANDI
      SHL DL,1 ;TUS YOKSA DIGER SATIRI TARA
   LOOP L2
   
   SHR BL,1 ;SIRADAKI SUTUN AKTIF 
   INC SI ;DIS DONGU 
CMP SI,4
JB L1

JMP MAINLOOP
;TUS ALGILANDI
TUS_ALGILANDI:
;AYNI TUSU ARKA ARKAYA ALMAMASI ICIN DELAY
PUSH CX
CALL WAIT_A_LITTLE
POP CX
CALL TUSU_TESPIT_ET   
;TUS AX DE DONDU

;BASILAN TUS * ISE 
CMP AX ,10
JNZ DEGIL
;* ISE TOTAL SIFIRLANACAK
MOV TOTAL,0
JE MAINLOOP

DEGIL:
;BASILAN TUS # ISE
CMP AX,12
JNZ DEGIL2
JMP MAINLOOP ; # ISE HIC BIR SEY YAPMA

DEGIL2:
;BASILAN TUS RAKAM ISE
MOV DL,TOTAL
CMP TOTAL,4
JB TUSU_KAYDET ; 4TEN AZ TUS GIRILDIYSE
CALL SAYILARI_KAYDIR     ; ZATEN 4 TUS VARSA KAYDIR
JMP MAINLOOP
TUSU_KAYDET:
MOV BL,TOTAL
MOV INPUTS[BX],AL
INC BL
MOV TOTAL,BL


JMP MAINLOOP

RET
START ENDP

;;;;;;;;;;;;;;;;;;;
TUSU_TESPIT_ET PROC NEAR
;SONUC AX ILE DONDURULUR
;(4-CX)* 3 + SI BAGINTISI ILE KACINCI TUS 
;OLDUGU TESPIT EDILEBILIR
MOV AX,4 
SUB AX,CX  
MOV BL,3
MUL BL
ADD AX,SI 

RET
TUSU_TESPIT_ET ENDP
;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SAYILARI_YAZDIR PROC NEAR
MOV SI,0
MOV DL,01H
L4:
   XOR BX,BX
   MOV BL,INPUTS[SI] ;RAKAMIN INT DEGERI BX TE
   
   MOV AL,DL
   OUT 0ABH ,AL  ;PORT B ILE 7 SEGMENTIN ILGILI KISMI AKTIFLESTIRILIYOR 
   MOV AL, DIGITS[BX]  ; RAKAMIN 7 SEGMENT DEGERI AL DE
   OUT 0A9H,AL	;PORT A
   MOV CX, 001FH
   DELAY: LOOP DELAY
   INC SI
   SHL DL,1

CMP SI,3
JBE L4  ;SI 4 OLUNCA DONGUDEN CIK 


RET
SAYILARI_YAZDIR ENDP
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SAYILARI_KAYDIR PROC NEAR

PUSH AX ; SON OKUNAN SAYI YIGINDA

XOR AX,AX
MOV CX,3
MOV SI,1
L5:
MOV AL,INPUTS[SI]
MOV INPUTS[SI-1],AL
INC SI
LOOP L5

POP AX ;SON SAYI YIGINDAN CEKILDI
MOV INPUTS[3],AL ;SONA EKLENDI

RET
SAYILARI_KAYDIR ENDP
;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
WAIT_A_LITTLE PROC NEAR

;Remember to save previous values
;Looping to spend sometime

MOV CX, 0FFFH
LOOP1: LOOP LOOP1

RET
WAIT_A_LITTLE ENDP
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CODE    ENDS
        END START